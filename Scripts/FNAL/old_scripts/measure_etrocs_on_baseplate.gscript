COPY $SLO_SPEED 10
COPY $LO_SPEED 30
COPY $HI_SPEED 50

SETVAC baseplates_chuck 1
WAIT 6000
HOME
MOVENAME ETL_baseplate_chuck_1_slot_1 $HI_SPEED
COPY $COUNT_ALL 10 # Number of loops
SETLIGHT 50

SETLOG "garbage.txt"
CLEARLOG

@BEGINLOOP
PRINT %d $COUNT_ALL

COPY $EC1 {-11.750,-10.700,0} # ETROC center 1 (z component is irrelevant and is set to zero)
COPY $EC2 {11.750,-10.700,0} # ETROC center 2 (z component is irrelevant and is set to zero)
COPY $EC3 {-11.750,10.700,0} # ETROC center 3 (z component is irrelevant and is set to zero)
COPY $EC4 {11.750,10.700,0} # ETROC center 4 (z component is irrelevant and is set to zero)

COPY $EF1 {31.082000,56.671000,95.140800} # ETROC Fiducial 1
COPY $EF2 {74.616000,56.235000,95.113800} # ETROC Fiducial 2
COPY $EF3 {31.060000,78.069000,95.125800} # ETROC Fiducial 3
COPY $EF4 {74.569000,77.599000,95.111800} # ETROC Fiducial 4

SETLOG "raw_2021_11_11_1.txt"

COPY $fidn 1 # Fiducial photo filename numbers
MOVENAME ETL_baseplate_chuck_1_slot_1 $HI_SPEED
CALL @CALC_BASEPLATE_CENTER_ROT
COPY $baseplate_one_pos $baseplate_pos
QUAT2EULER $baseplate_one_yaw $pitch $roll $baseplate_rot

COPY $etn 1 # Fiducial photo filename numbers
COPY $fidn 1 # Fiducial photo filename numbers
MOVETO $EF1 $SLO_SPEED
CALL @CALC_ETROC_CENTER_ROT
COPY $etroc_one_pos $etroc_pos
QUAT2EULER $etroc_one_yaw $pitch $roll $etroc_rot

INC $etn $etn
COPY $fidn 1
MOVETO $EF2 $SLO_SPEED
CALL @CALC_ETROC_CENTER_ROT_INV
COPY $etroc_two_pos $etroc_pos
QUAT2EULER $etroc_two_yaw $pitch $roll $etroc_rot
COPY $etroc_yaw $etroc_two_yaw
CALL @ANGLE_FIX
COPY $etroc_two_yaw $etroc_yaw

INC $etn $etn
COPY $fidn 1
MOVETO $EF3 $SLO_SPEED
CALL @CALC_ETROC_CENTER_ROT
COPY $etroc_three_pos $etroc_pos
QUAT2EULER $etroc_three_yaw $pitch $roll $etroc_rot

INC $etn $etn
COPY $fidn 1
MOVETO $EF4 $SLO_SPEED
CALL @CALC_ETROC_CENTER_ROT_INV
COPY $etroc_four_pos $etroc_pos
QUAT2EULER $etroc_four_yaw $pitch $roll $etroc_rot
COPY $etroc_yaw $etroc_four_yaw
CALL @ANGLE_FIX
COPY $etroc_four_yaw $etroc_yaw

SUB $etroc_one_pos $etroc_one_pos $baseplate_one_pos
SUB $etroc_two_pos $etroc_two_pos $baseplate_one_pos
SUB $etroc_three_pos $etroc_three_pos $baseplate_one_pos
SUB $etroc_four_pos $etroc_four_pos $baseplate_one_pos

SUB $etroc_one_yaw $etroc_one_yaw $baseplate_one_yaw
SUB $etroc_two_yaw $etroc_two_yaw $baseplate_one_yaw
SUB $etroc_three_yaw $etroc_three_yaw $baseplate_one_yaw
SUB $etroc_four_yaw $etroc_four_yaw $baseplate_one_yaw

COPY $bpyaw $baseplate_one_yaw
COPY $V $EC1
CALL @ROT_CORRECT
COPY $EC1 $VROT
COPY $V $EC2
CALL @ROT_CORRECT
COPY $EC2 $VROT
COPY $V $EC3
CALL @ROT_CORRECT
COPY $EC3 $VROT
COPY $V $EC4
CALL @ROT_CORRECT
COPY $EC4 $VROT

SUB $dev1 $etroc_one_pos $EC1 # Deviation from expected position of ETROC 1
SUB $dev2 $etroc_two_pos $EC2 # Deviation from expected position of ETROC 2
SUB $dev3 $etroc_three_pos $EC3 # Deviation from expected position of ETROC 3
SUB $dev4 $etroc_four_pos $EC4 # Deviation from expected position of ETROC 4

#PRINT "Position deviations of ETROCs:"
#PRINT %v $dev1
#PRINT %v $dev2
#PRINT %v $dev3
#PRINT %v $dev4
#PRINT "ETROC angles rel. to baseplate:"
#PRINT %f $etroc_one_yaw
#PRINT %f $etroc_two_yaw
#PRINT %f $etroc_three_yaw
#PRINT %f $etroc_four_yaw
#PRINT "Writing to file:"
SETLOG "2021_11_11_1.txt"
PRINT %v {$dev1.x,$dev1.y,$etroc_one_yaw}
PRINT %v {$dev2.x,$dev2.y,$etroc_two_yaw}
PRINT %v {$dev3.x,$dev3.y,$etroc_three_yaw}
PRINT %v {$dev4.x,$dev4.y,$etroc_four_yaw}
SETLOG "garbage.txt"

DEC $COUNT_ALL $COUNT_ALL
GOTOIF @BEGINLOOP $COUNT_ALL

SETVAC baseplates_chuck 0
SETLIGHT 0
HOME
END

@CALC_BASEPLATE_CENTER_ROT COPY $BREAK $RET
    FLEXREAD $fid_dx geometry.ETL_Baseplate_PCB_Mockup.fid_dx
    FLEXREAD $fid_dy geometry.ETL_Baseplate_PCB_Mockup.fid_dy

    WAIT 3500 # y-axis stabilization
    CALL @BASEPLATE_FID_FINDER
    PRINT %v $baseplate_fid
    COPY $baseplate_tl $baseplate_fid

    MOVEREL $fid_dx $SLO_SPEED
    WAIT 200 # x-axis stabilization
    CALL @BASEPLATE_FID_FINDER
    PRINT %v $baseplate_fid
    COPY $baseplate_tr $baseplate_fid

    MOVEREL $fid_dy $SLO_SPEED
    WAIT 1500 # y-axis stabilization
    CALL @BASEPLATE_FID_FINDER
    PRINT %v $baseplate_fid
    COPY $baseplate_br $baseplate_fid

    MUL $fid_dx -1 $fid_dx
    MOVEREL $fid_dx $SLO_SPEED
    WAIT 200 # x-axis stabilization
    CALL @BASEPLATE_FID_FINDER
    PRINT %v $baseplate_fid
    COPY $baseplate_bl $baseplate_fid

    FIT $baseplate_pos $baseplate_rot ETL_Baseplate_PCB_Mockup $baseplate_tr $baseplate_br $baseplate_bl $baseplate_tl
    GOTO $BREAK

@BASEPLATE_FID_FINDER COPY $BREAKOUT $RET
    SETLIGHT 20
    AUTOFOCUS $z 0.4 autoclose ##
    SNAPSHOT gantryhead "fid\{$COUNT_ALL}\baseplate_{$fidn}.png"
    INC $fidn $fidn
    #VIDEO
    SETLIGHT 50
    SETERRORMODE setvar
    COPY $RETRIES 3
    @RETRY_BASEPLATE DEC $RETRIES $RETRIES
    GOTOIFN @MANUAL_FID_BASEPLATE $RETRIES
    FINDFID $baseplate_fid ETL_Baseplate_PCB_Mockup 
    GOTOIF @RETRY_BASEPLATE $ERR
    SETERRORMODE normal
    GOTO $BREAKOUT
    @MANUAL_FID_BASEPLATE CHOICEPOPUP $c "Unable to locate fiducial, use camera feed and mpg to manually center the gantry on the fiducial" "continue" "continue"
    PRINT "Fiducial not detected. If the fiducial is not visible in the frame, make the fiducial completely visible."
    MPGON
    VIDEO
    PRINT "FINDFID for ..."
    FINDFID $baseplate_fid ETL_Baseplate_PCB_Mockup 
    MPGOFF
    GOTOIF @FINAL_RETRY_BASEPLATE $ERR
    GOTO $BREAKOUT
    @FINAL_RETRY_BASEPLATE COPY $BREAK_FINAL_RETRY $RET
    PRINT "Fiducial not detected, move the crosshair EXACTLY to the fiducial center."
    MPGON
    VIDEO
    PRINT "GETPOS for ..."
    GETPOS $baseplate_fid
    MPGOFF
    GOTO $BREAKOUT

@CALC_ETROC_CENTER_ROT COPY $BREAK $RET
    FLEXREAD $fid_tl_tr geometry.ETL_ETROC_PCB_Mockup.fid_tl_tr
    FLEXREAD $fid_tr_br geometry.ETL_ETROC_PCB_Mockup.fid_tr_br
    FLEXREAD $fid_br_bl geometry.ETL_ETROC_PCB_Mockup.fid_br_bl

    WAIT 1500 # y-axis stabilization
    CALL @ETROC_FID_FINDER
    PRINT %v $etroc_fid
    COPY $etroc_tl $etroc_fid

    MOVEREL $fid_tl_tr $SLO_SPEED
    WAIT 200 # x-axis stabilization
    CALL @ETROC_FID_FINDER
    PRINT %v $etroc_fid
    COPY $etroc_tr $etroc_fid

    MOVEREL $fid_tr_br $SLO_SPEED
    WAIT 1500 # y-axis stabilization
    CALL @ETROC_FID_FINDER
    PRINT %v $etroc_fid
    COPY $etroc_br $etroc_fid

    MOVEREL $fid_br_bl $SLO_SPEED
    WAIT 200 # x-axis stabilization
    CALL @ETROC_FID_FINDER
    PRINT %v $etroc_fid
    COPY $etroc_bl $etroc_fid

    FIT $etroc_pos $etroc_rot ETL_ETROC_PCB_Mockup $etroc_tr $etroc_br $etroc_bl $etroc_tl
    GOTO $BREAK

@CALC_ETROC_CENTER_ROT_INV COPY $BREAK_INV $RET
    FLEXREAD $fid_tl_tr geometry.ETL_ETROC_PCB_Mockup.fid_tl_tr
    FLEXREAD $fid_tr_br geometry.ETL_ETROC_PCB_Mockup.fid_tr_br
    FLEXREAD $fid_br_bl geometry.ETL_ETROC_PCB_Mockup.fid_br_bl

    WAIT 1500 # y-axis stabilization
    CALL @ETROC_FID_FINDER
    PRINT %v $etroc_fid
    COPY $etroc_bl $etroc_fid

    MOVEREL $fid_br_bl $SLO_SPEED
    WAIT 200 # x-axis stabilization
    CALL @ETROC_FID_FINDER
    PRINT %v $etroc_fid
    COPY $etroc_br $etroc_fid

    MOVEREL $fid_tr_br $SLO_SPEED
    WAIT 1500 # y-axis stabilization
    CALL @ETROC_FID_FINDER
    PRINT %v $etroc_fid
    COPY $etroc_tr $etroc_fid

    MOVEREL $fid_tl_tr $SLO_SPEED
    WAIT 200 # x-axis stabilization
    CALL @ETROC_FID_FINDER
    PRINT %v $etroc_fid
    COPY $etroc_tl $etroc_fid

    FIT $etroc_pos $etroc_rot ETL_ETROC_PCB_Mockup $etroc_tr $etroc_br $etroc_bl $etroc_tl
    GOTO $BREAK_INV

@ETROC_FID_FINDER COPY $BREAKOUT $RET
    SETLIGHT 15
    AUTOFOCUS $z 0.4 autoclose ##
    SNAPSHOT gantryhead "fid7\{$COUNT_ALL}\etroc_{$etn}_{$fidn}.png"
    INC $fidn $fidn
    #VIDEO
    SETLIGHT 50
    SETERRORMODE setvar
    COPY $RETRIES 3
    @RETRY_ETROC DEC $RETRIES $RETRIES
    GOTOIFN @MANUAL_FID_ETROC $RETRIES
    FINDFID $etroc_fid ETL_ETROC_PCB_Mockup
    GOTOIF @RETRY_ETROC $ERR
    SETERRORMODE normal
    GOTO $BREAKOUT
    @MANUAL_FID_ETROC CHOICEPOPUP $c "Unable to locate fiducial, use camera feed and mpg to manually center the gantry on the fiducial" "continue" "continue"
    PRINT "Fiducial not detected. If the fiducial is not fully visible in the frame, make it completely visible."
    MPGON
    VIDEO
    PRINT "FINDFID for ETROC:"
    FINDFID $etroc_fid ETL_ETROC_PCB_Mockup
    MPGOFF
    GOTOIF @FINAL_RETRY_ETROC $ERR
    SETERRORMODE normal
    GOTO $BREAKOUT
    @FINAL_RETRY_ETROC COPY $BREAK_FINAL_RETRY $RET
    PRINT "Fiducial not detected, move the crosshair EXACTLY to the fiducial center."
    MPGON
    VIDEO
    PRINT "GETPOS for ETROC:"
    GETPOS $etroc_fid
    MPGOFF
    SETERRORMODE normal
    GOTO $BREAKOUT

@CALC_SENSOR_CENTER_ROT COPY $BREAK $RET
    FLEXREAD $fid_dx geometry.ETL_Sensor_PCB_Mockup.fid_dx
    FLEXREAD $fid_dy geometry.ETL_Sensor_PCB_Mockup.fid_dy

    CALL @SENSOR_FID_FINDER
    COPY $Sensor_tl $Sensor_fid

    MOVEREL $fid_dx $LO_SPEED
    CALL @SENSOR_FID_FINDER
    COPY $Sensor_tr $Sensor_fid

    MOVEREL $fid_dy $LO_SPEED
    CALL @SENSOR_FID_FINDER
    COPY $Sensor_br $Sensor_fid

    MUL $fid_dx -1 $fid_dx
    MOVEREL $fid_dx $LO_SPEED

    CALL @SENSOR_FID_FINDER
    COPY $Sensor_bl $Sensor_fid

    FIT $sensor_pos $sensor_rot ETL_Sensor_PCB_Mockup $Sensor_tr $Sensor_br $Sensor_bl $Sensor_tl
    GOTO $BREAK

@SENSOR_FID_FINDER COPY $BREAKOUT $RET
    # VIDEO
    # AUTOFOCUS $z 0.5 autoclose
    # SETLIGHT 10
    SETERRORMODE setvar
    COPY $RETRIES 3
    @RETRY_SENSOR DEC $RETRIES $RETRIES
    GOTOIFN @MANUAL_FID_SENS $RETRIES
    FINDFID $Sensor_fid ETL_Sensor_PCB_Mockup 
    GOTOIF @RETRY_SENSOR $ERR
    SETERRORMODE normal
    GOTO $BREAKOUT
    @MANUAL_FID_SENS CHOICEPOPUP $c "Unable to locate fiducial, use camera feed and mpg to manually center the gantry on the fiducial" "continue" "continue"
    MPGON
    VIDEO
    GETPOS $Sensor_fid
    MPGOFF
    GOTO $BREAKOUT

@PICKUP COPY $EXITPICKUP $RET
    FLEXREAD $pickup_tho geometry.tool_holder_offset
    FLEXREAD $pickup_co geometry.PICKER_TOOL.center_offset
    FLEXREAD $camera_to_obj geometry.camera_focused_to_object

    ADD $pickup_pos $pickup_pos $pickup_tho
    ADD $pickup_pos $pickup_pos $pickup_co
    ADD $pickup_pos $pickup_pos $camera_to_obj

    print %v $pickup_pos
    MOVETO $pickup_pos $LO_SPEED
    MOVEREL {0,0,1.5} 1
    WAIT 1000
    SETVAC gantry_head_inner 1
    WAIT 1000
    CALL @vacuum_control
    WAIT 1000
    MOVEREL {0,0,-20} 10
    GOTO $EXITPICKUP

@vacuum_control COPY $VACEXIT $RET
    SUB $chan1 $vachan 1
    SUB $chan2 $vachan 2
    SUB $chan3 $vachan 3
    SUB $chan4 $vachan 4
    SUB $chan5 $vachan 5
    SUB $chan6 $vachan 6
    SUB $chan7 $vachan 7
    SUB $chan8 $vachan 8

    GOTOIFN @turn_off_chan_1 $chan1
    GOTOIFN @turn_off_chan_2 $chan2
    GOTOIFN @turn_off_chan_3 $chan3
    GOTOIFN @turn_off_chan_4 $chan4
    GOTOIFN @turn_off_chan_5 $chan5
    GOTOIFN @turn_off_chan_6 $chan6
    GOTOIFN @turn_off_chan_7 $chan7
    GOTOIFN @turn_off_chan_8 $chan8

    @turn_off_chan_1 setvac sensor_1_chuck 0
    GOTO $VACEXIT 
    @turn_off_chan_2 setvac sensor_2_chuck 0
    GOTO $VACEXIT 
    @turn_off_chan_3 setvac sensor_3_chuck 0
    GOTO $VACEXIT 
    @turn_off_chan_4 setvac sensor_4_chuck 0
    GOTO $VACEXIT 
    @turn_off_chan_5 setvac sensor_5_chuck 0
    GOTO $VACEXIT 
    @turn_off_chan_6 setvac sensor_6_chuck 0
    GOTO $VACEXIT 
    @turn_off_chan_7 setvac sensor_7_chuck 0
    GOTO $VACEXIT 
    @turn_off_chan_8 setvac sensor_8_chuck 0
    GOTO $VACEXIT 

@PLACE COPY $PLACEEXIT $RET
    FLEXREAD $place_tho geometry.tool_holder_offset
    FLEXREAD $place_co geometry.PICKER_TOOL.center_offset
    FLEXREAD $camera_to_obj geometry.camera_focused_to_object
    ADD $place_pos $place_pos $place_tho
    ADD $place_pos $place_pos $place_co
    ADD $place_pos $place_pos $camera_to_obj
    ADD $place_pos $place_pos {0,0,-7}
    print %v $place_pos

    ADD $place_pos $place_pos $offset

    MOVETO $place_pos $LO_SPEED
    MOVEREL {0,0,8.5} 1
    WAIT 1000
    SETVAC gantry_head_inner 0
    WAIT 1000
    MOVEREL {0,0,-20} 10
    GOTO $PLACEEXIT

@ROT_CORRECT COPY $ROTCOREXIT $RET
    COS $C $bpyaw
    SIN $S $bpyaw
    MUL $XM $C $V.x
    MUL $YM $S $V.y
    SUB $XR $XM $YM
    MUL $XM $S $V.x
    MUL $YM $C $V.y
    ADD $YR $YM $XM
    COPY $VROT {$XR,$YR,$V.z}
    GOTO $ROTCOREXIT

    # Angles of the ETROCs on the right side are shown as either ~179 deg. (ccw deviation) or ~-179 deg. (cw deviation). ANGLE_FIX function fixes this problem with angle values.
@ANGLE_FIX COPY $AFEXIT $RET
    ABS $one80 $etroc_yaw
    INVERT $one80 $one80
    MUL $one80 $one80 -180
    MUL $one80 $one80 $etroc_yaw
    ADD $etroc_yaw $etroc_yaw $one80
    GOTO $AFEXIT