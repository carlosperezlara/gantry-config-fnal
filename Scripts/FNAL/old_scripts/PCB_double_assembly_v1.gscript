#HOME
SETLOG "dummy_assembly.txt"

SETVAC chuck_0 1
SETVAC chuck_1 1

COPY $LO_SPEED 30
COPY $HI_SPEED 50

MOVENAME ETL_baseplate_chuck_0_slot_0 $HI_SPEED
CALL @CALC_BASEPLATE_CENTER_ROT
COPY $baseplate_one_pos $baseplate_pos
COPY $baseplate_one_rot $baseplate_rot


PRINT %v $baseplate_one_pos

MOVENAME ETL_baseplate_chuck_0_slot_1 $HI_SPEED
CALL @CALC_BASEPLATE_CENTER_ROT
COPY $baseplate_two_pos $baseplate_pos
COPY $baseplate_two_rot $baseplate_rot

PRINT %v $baseplate_two_pos

MOVENAME ETL_sensor_chuck_0_slot_0 $HI_SPEED
CALL @CALC_SENSOR_CENTER_ROT
COPY $sensor_one_pos $sensor_pos
COPY $sensor_one_rot $sensor_rot

PRINT %v $sensor_one_pos

MOVENAME ETL_sensor_chuck_0_slot_1 $HI_SPEED
CALL @CALC_SENSOR_CENTER_ROT
COPY $sensor_two_pos $sensor_pos
COPY $sensor_two_rot $sensor_rot

PRINT %v $sensor_two_pos

MOVENAME ETL_sensor_chuck_0_slot_2 $HI_SPEED
CALL @CALC_SENSOR_CENTER_ROT
COPY $sensor_three_pos $sensor_pos
COPY $sensor_three_rot $sensor_rot

PRINT %v $sensor_three_pos

MOVENAME ETL_sensor_chuck_0_slot_3 $HI_SPEED
CALL @CALC_SENSOR_CENTER_ROT
COPY $sensor_four_pos $sensor_pos
COPY $sensor_four_rot $sensor_rot

PRINT %v $sensor_four_pos

LOADTOOL picker_tool

# top sensor first module
COPY $place_pos $baseplate_one_pos
COPY $place_rot $baseplate_one_rot
COPY $pickup_pos $sensor_one_pos
CALL @PICKUP 
COPY $offset -11.775
COPY $pickup_rot $sensor_one_rot
CALL @PLACE

# bottom sensor first module
COPY $place_pos $baseplate_one_pos
COPY $place_rot $baseplate_one_rot
COPY $pickup_pos $sensor_two_pos
CALL @PICKUP
COPY $offset 11.775
COPY $pickup_rot $sensor_two_rot
CALL @PLACE

# top sensor second module
COPY $place_pos $baseplate_two_pos
COPY $place_rot $baseplate_two_rot
COPY $pickup_pos $sensor_three_pos
CALL @PICKUP 
COPY $offset -11.775
COPY $pickup_rot $sensor_three_rot
CALL @PLACE

# bottom sensor second module
COPY $place_pos $baseplate_two_pos
COPY $place_rot $baseplate_two_rot
COPY $pickup_pos $sensor_four_pos
CALL @PICKUP
COPY $offset 11.775
COPY $pickup_rot $sensor_four_rot
CALL @PLACE

UNLOADTOOL

MOVENAME home $HI_SPEED

SETVAC chuck_0 0
SETVAC chuck_1 0

END

@CALC_BASEPLATE_CENTER_ROT COPY $BREAK $RET
FLEXREAD $fid_dx geometry.ETL_Baseplate_PCB_Mockup.fid_dx
FLEXREAD $fid_dy geometry.ETL_Baseplate_PCB_Mockup.fid_dy

CALL @BASEPLATE_FID_FINDER
COPY $baseplate_tl $baseplate_fid

MOVEREL $fid_dx $LO_SPEED

CALL @BASEPLATE_FID_FINDER
COPY $baseplate_tr $baseplate_fid

MOVEREL $fid_dy $LO_SPEED
CALL @BASEPLATE_FID_FINDER
COPY $baseplate_br $baseplate_fid

MUL $fid_dx -1 $fid_dx
MOVEREL $fid_dx $LO_SPEED

CALL @BASEPLATE_FID_FINDER
COPY $baseplate_bl $baseplate_fid

FIT $baseplate_pos $baseplate_rot ETL_Baseplate_PCB_Mockup $baseplate_tr $baseplate_br $baseplate_bl $baseplate_tl
GOTO $BREAK

@BASEPLATE_FID_FINDER COPY $BREAKOUT $RET
    # VIDEO
    AUTOFOCUS $z 0.5 autoclose
    SETLIGHT 40
    SETERRORMODE setvar
    COPY $RETRIES 3
    @RETRY_BASEPLATE DEC $RETRIES $RETRIES
    GOTOIFN @MANUAL_FID_BP $RETRIES
    FINDFID $baseplate_fid ETL_Baseplate_PCB_Mockup
    GOTOIF @RETRY_BASEPLATE $ERR
    SETERRORMODE normal
    GOTO $BREAKOUT
    @MANUAL_FID_BP CHOICEPOPUP $c "Unable to locate fiducial, use camera feed and mpg to manually center the gantry on the fiducial" "continue" "continue"
    MPGON
    VIDEO
    GETPOS $baseplate_fid
    MPGOFF
    GOTO $BREAKOUT

@CALC_SENSOR_CENTER_ROT COPY $BREAK $RET
FLEXREAD $fid_dx geometry.ETL_Sensor_PCB_Mockup.fid_dx
FLEXREAD $fid_dy geometry.ETL_Sensor_PCB_Mockup.fid_dy

CALL @SENSOR_FID_FINDER
COPY $Sensor_tl $Sensor_fid

MOVEREL $fid_dx $LO_SPEED
CALL @SENSOR_FID_FINDER
COPY $Sensor_tr $Sensor_fid

MOVEREL $fid_dy $LO_SPEED
CALL @SENSOR_FID_FINDER
COPY $Sensor_br $Sensor_fid

MUL $fid_dx -1 $fid_dx
MOVEREL $fid_dx $LO_SPEED

CALL @SENSOR_FID_FINDER
COPY $Sensor_bl $Sensor_fid

FIT $sensor_pos $sensor_rot ETL_Sensor_PCB_Mockup $Sensor_tr $Sensor_br $Sensor_bl $Sensor_tl
GOTO $BREAK

@SENSOR_FID_FINDER COPY $BREAKOUT $RET
    # VIDEO
    AUTOFOCUS $z 0.5 autoclose
    SETLIGHT 10
    SETERRORMODE setvar
    COPY $RETRIES 3
    @RETRY_SENSOR DEC $RETRIES $RETRIES
    GOTOIFN @MANUAL_FID_SENS $RETRIES
    FINDFID $Sensor_fid ETL_Sensor_PCB_Mockup 
    GOTOIF @RETRY_SENSOR $ERR
    SETERRORMODE normal
    GOTO $BREAKOUT
    @MANUAL_FID_SENS CHOICEPOPUP $c "Unable to locate fiducial, use camera feed and mpg to manually center the gantry on the fiducial" "continue" "continue"
    MPGON
    VIDEO
    GETPOS $Sensor_fid
    MPGOFF
    GOTO $BREAKOUT

@PICKUP PASS
    FLEXREAD $pickup_tho geometry.tool_holder_offset
    FLEXREAD $pickup_co geometry.PICKER_TOOL.center_offset
    SUB $pickup_pos $pickup_pos $pickup_tho
    SUB $pickup_pos $pickup_pos $pickup_co
    print %v $pickup_pos
    MOVETO $pickup_pos $LO_SPEED
    MOVEREL {0,0,1.4} 1
    WAIT 1000
    SETVAC gantry_head_inner 1
    WAIT 2000
    MOVEREL {0,0,-20} 10
    GOTO $RET

@PLACE PASS
    FLEXREAD $place_tho geometry.tool_holder_offset
    FLEXREAD $place_co geometry.PICKER_TOOL.center_offset
    SUB $place_pos $place_pos $place_tho
    SUB $place_pos $place_pos $place_co
    print %v $place_pos

    QUAT2EULER $pickup_yaw $p $r $pickup_rot
    ROTATE $pickup_yaw 5

    INVERT $place_rot_inv $place_rot

    QUAT2EULER $place_yaw $p $r $place_rot_inv
    ROTATE $place_yaw 5

    QUAT2EULER $baseplate_yaw $p $r $place_rot

    COS $cos_place $baseplate_yaw
    SIN $sin_place $baseplate_yaw

    MUL $dx_place $offset $sin_place
    MUL $dx_place $dx_place -1
    MUL $dy_place $offset $cos_place

    ADD $place_pos.x $place_pos.x $dx_place
    ADD $place_pos.y $place_pos.y $dy_place
    
    MOVETO $place_pos $LO_SPEED
    MOVEREL {0,0,.95} 1
    WAIT 2000
    SETVAC gantry_head_inner 0
    WAIT 2000
    MOVEREL {0,0,-20} 10
    ROTATETO 0 5
    GOTO $RET