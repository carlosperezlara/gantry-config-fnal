HOME
SETLOG "assembly_aug_30.txt"

SETLIGHT 50

setvac sensor_1_chuck 1
setvac sensor_2_chuck 1
setvac sensor_3_chuck 1
setvac sensor_4_chuck 1
#setvac sensor_5_chuck 1
#setvac sensor_6_chuck 1
#setvac sensor_7_chuck 1
#setvac sensor_8_chuck 1


COPY $LO_SPEED 30
COPY $HI_SPEED 50

MOVENAME ETL_baseplate_chuck_1_slot_1 $HI_SPEED
CALL @CALC_BASEPLATE_CENTER_ROT
COPY $baseplate_one_pos $baseplate_pos
QUAT2EULER $bp_one_yaw $pitch $roll $baseplate_rot
PRINT %v $baseplate_pos

MOVENAME ETL_combo_chuck_1_slot_1 $HI_SPEED
CALL @CALC_SENSOR_CENTER_ROT
COPY $combo_one_pos $sensor_pos
QUAT2EULER $combo_one_yaw $pitch $roll $sensor_rot
PRINT %v $combo_one_pos

MOVENAME ETL_combo_chuck_1_slot_2 $HI_SPEED
CALL @CALC_SENSOR_CENTER_ROT
COPY $combo_two_pos $sensor_pos
QUAT2EULER $combo_two_yaw $pitch $roll $sensor_rot
PRINT %v $combo_two_pos

MOVENAME ETL_combo_chuck_1_slot_3 $HI_SPEED
CALL @CALC_SENSOR_CENTER_ROT
COPY $combo_three_pos $sensor_pos
QUAT2EULER $combo_three_yaw $pitch $roll $sensor_rot
PRINT %v $combo_three_pos

MOVENAME ETL_combo_chuck_1_slot_4 $HI_SPEED
CALL @CALC_SENSOR_CENTER_ROT
COPY $combo_four_pos $sensor_pos
QUAT2EULER $combo_four_yaw $pitch $roll $sensor_rot
PRINT %v $combo_four_pos

LOADTOOL picker_tool

# first combo onto baseplate
COPY $place_pos $baseplate_one_pos
COPY $pickup_pos $combo_one_pos
copy $vachan 1
CALL @PICKUP
SUB $ANGDIFF $combo_one_yaw $bp_one_yaw
ROTATE $ANGDIFF 0.5
COPY $offset {-10.9,-10.7,0}
CALL @PLACE

# second combo onto baseplate
COPY $place_pos $baseplate_one_pos
COPY $pickup_pos $combo_two_pos
copy $vachan 2
CALL @PICKUP
SUB $ANGDIFF $combo_two_yaw $bp_one_yaw
ROTATE $ANGDIFF 0.5
COPY $offset {10.9,-10.7,0}
CALL @PLACE

# third combo onto baseplate
COPY $place_pos $baseplate_one_pos
COPY $pickup_pos $combo_three_pos
copy $vachan 3
CALL @PICKUP
SUB $ANGDIFF $combo_three_yaw $bp_one_yaw
ROTATE $ANGDIFF 0.5
COPY $offset {-10.9,10.7,0}
CALL @PLACE

# fourth combo onto baseplate
COPY $place_pos $baseplate_one_pos
COPY $pickup_pos $combo_four_pos
copy $vachan 4
CALL @PICKUP
SUB $ANGDIFF $combo_four_yaw $bp_one_yaw
ROTATE $ANGDIFF 0.5
COPY $offset {10.9,10.7,0}
CALL @PLACE

UNLOADTOOL

MOVENAME home $HI_SPEED

SETLIGHT 0

END

@CALC_BASEPLATE_CENTER_ROT COPY $BREAK $RET
    FLEXREAD $fid_dx geometry.ETL_Baseplate_PCB_Mockup.fid_dx
    FLEXREAD $fid_dy geometry.ETL_Baseplate_PCB_Mockup.fid_dy

    CALL @BASEPLATE_FID_FINDER
    COPY $baseplate_tl $baseplate_fid

    MOVEREL $fid_dx $LO_SPEED
    CALL @BASEPLATE_FID_FINDER
    COPY $baseplate_tr $baseplate_fid

    MOVEREL $fid_dy $LO_SPEED
    CALL @BASEPLATE_FID_FINDER
    COPY $baseplate_br $baseplate_fid

    MUL $fid_dx -1 $fid_dx
    MOVEREL $fid_dx $LO_SPEED

    CALL @BASEPLATE_FID_FINDER
    COPY $baseplate_bl $baseplate_fid

    FIT $baseplate_pos $baseplate_rot ETL_Baseplate_PCB_Mockup $baseplate_tr $baseplate_br $baseplate_bl $baseplate_tl
    GOTO $BREAK

@BASEPLATE_FID_FINDER COPY $BREAKOUT $RET
    # VIDEO
    # AUTOFOCUS $z 0.5 autoclose
    # SETLIGHT 10
    SETERRORMODE setvar
    COPY $RETRIES 3
    @RETRY_BASEPLATE DEC $RETRIES $RETRIES
    GOTOIFN @MANUAL_FID_BASEPLATE $RETRIES
    FINDFID $baseplate_fid ETL_Baseplate_PCB_Mockup
    GOTOIF @RETRY_BASEPLATE $ERR
    SETERRORMODE normal
    GOTO $BREAKOUT
    @MANUAL_FID_BASEPLATE CHOICEPOPUP $c "Unable to locate fiducial, use camera feed and mpg to manually center the gantry on the fiducial" "continue" "continue"
    PRINT "Fiducial not detected. If the fiducial is not fully visible in the frame, make it completely visible."
    MPGON
    VIDEO
    PRINT "FINDFID for baseplate:"
    FINDFID $baseplate_fid ETL_Baseplate_PCB_Mockup
    GOTOIF @FINAL_RETRY_BASEPLATE $ERR
    SETERRORMODE normal
    GOTO $BREAKOUT
    @FINAL_RETRY_BASEPLATE COPY $BREAK_FINAL_RETRY $RET
    PRINT "Fiducial not detected, move the crosshair EXACTLY to the fiducial center."
    MPGON
    VIDEO
    PRINT "GETPOS for baseplate:"
    GETPOS $baseplate_fid
    MPGOFF
    SETERRORMODE normal
    GOTO $BREAKOUT


@CALC_SENSOR_CENTER_ROT COPY $BREAK $RET
    FLEXREAD $fid_dx geometry.ETL_Sensor_PCB_Mockup.fid_dx
    FLEXREAD $fid_dy geometry.ETL_Sensor_PCB_Mockup.fid_dy

    CALL @SENSOR_FID_FINDER
    COPY $Sensor_tl $Sensor_fid

    MOVEREL $fid_dx $LO_SPEED
    CALL @SENSOR_FID_FINDER
    COPY $Sensor_tr $Sensor_fid

    MOVEREL $fid_dy $LO_SPEED
    CALL @SENSOR_FID_FINDER
    COPY $Sensor_br $Sensor_fid

    MUL $fid_dx -1 $fid_dx
    MOVEREL $fid_dx $LO_SPEED
    CALL @SENSOR_FID_FINDER
    COPY $Sensor_bl $Sensor_fid

    FIT $sensor_pos $sensor_rot ETL_Sensor_PCB_Mockup $Sensor_tr $Sensor_br $Sensor_bl $Sensor_tl
    GOTO $BREAK

@SENSOR_FID_FINDER COPY $BREAKOUT $RET
    # VIDEO
    # AUTOFOCUS $z 0.5 autoclose
    # SETLIGHT 10
    SETERRORMODE setvar
    COPY $RETRIES 3
    @RETRY_SENSOR DEC $RETRIES $RETRIES
    GOTOIFN @MANUAL_FID_SENS $RETRIES
    FINDFID $Sensor_fid ETL_Sensor_PCB_Mockup 
    GOTOIF @RETRY_SENSOR $ERR
    SETERRORMODE normal
    GOTO $BREAKOUT
    @MANUAL_FID_SENS CHOICEPOPUP $c "Unable to locate fiducial, use camera feed and mpg to manually center the gantry on the fiducial" "continue" "continue"
    PRINT "Fiducial not detected. If the fiducial is not fully visible in the frame, make it completely visible."
    MPGON
    VIDEO
    PRINT "FINDFID for sensor:"
    FINDFID $Sensor_fid ETL_Sensor_PCB_Mockup
    GOTOIF @FINAL_RETRY_SENSOR $ERR
    SETERRORMODE normal
    GOTO $BREAKOUT
    @FINAL_RETRY_SENSOR COPY $BREAK_FINAL_RETRY $RET
    PRINT "Fiducial not detected, move the crosshair EXACTLY to the fiducial center."
    MPGON
    VIDEO
    PRINT "GETPOS for baseplate:"
    GETPOS $Sensor_fid
    MPGOFF
    SETERRORMODE normal
    GOTO $BREAKOUT

@PICKUP COPY $EXITPICKUP $RET
    FLEXREAD $pickup_tho geometry.tool_holder_offset
    FLEXREAD $pickup_co geometry.PICKER_TOOL.center_offset
    FLEXREAD $camera_to_obj geometry.camera_focused_to_object

    ADD $pickup_pos $pickup_pos $pickup_tho
    ADD $pickup_pos $pickup_pos $pickup_co
    ADD $pickup_pos $pickup_pos $camera_to_obj

    print %v $pickup_pos
    MOVETO $pickup_pos $LO_SPEED
    MOVEREL {0,0,1.5} 1
    WAIT 1000
    SETVAC gantry_head_inner 1
    WAIT 1000
    CALL @vacuum_control
    WAIT 1000
    MOVEREL {0,0,-20} 10
    GOTO $EXITPICKUP


@vacuum_control COPY $VACEXIT $RET
    SUB $chan1 $vachan 1
    SUB $chan2 $vachan 2
    SUB $chan3 $vachan 3
    SUB $chan4 $vachan 4
    SUB $chan5 $vachan 5
    SUB $chan6 $vachan 6
    SUB $chan7 $vachan 7
    SUB $chan8 $vachan 8

    GOTOIFN @turn_off_chan_1 $chan1
    GOTOIFN @turn_off_chan_2 $chan2
    GOTOIFN @turn_off_chan_3 $chan3
    GOTOIFN @turn_off_chan_4 $chan4
    GOTOIFN @turn_off_chan_5 $chan5
    GOTOIFN @turn_off_chan_6 $chan6
    GOTOIFN @turn_off_chan_7 $chan7
    GOTOIFN @turn_off_chan_8 $chan8

 

    @turn_off_chan_1 setvac sensor_1_chuck 0
    GOTO $VACEXIT 
    @turn_off_chan_2 setvac sensor_2_chuck 0
    GOTO $VACEXIT 
    @turn_off_chan_3 setvac sensor_3_chuck 0
    GOTO $VACEXIT 
    @turn_off_chan_4 setvac sensor_4_chuck 0
    GOTO $VACEXIT 
    @turn_off_chan_5 setvac sensor_5_chuck 0
    GOTO $VACEXIT 
    @turn_off_chan_6 setvac sensor_6_chuck 0
    GOTO $VACEXIT 
    @turn_off_chan_7 setvac sensor_7_chuck 0
    GOTO $VACEXIT 
    @turn_off_chan_8 setvac sensor_8_chuck 0
    GOTO $VACEXIT 

@PLACE COPY $PLACEEXIT $RET
    FLEXREAD $place_tho geometry.tool_holder_offset
    FLEXREAD $place_co geometry.PICKER_TOOL.center_offset
    FLEXREAD $camera_to_obj geometry.camera_focused_to_object
    ADD $place_pos $place_pos $place_tho
    ADD $place_pos $place_pos $place_co
    ADD $place_pos $place_pos $camera_to_obj
    ADD $place_pos $place_pos {0,0,-7}
    print %v $place_pos
    ADD $place_pos $place_pos $offset
    MOVETO $place_pos $LO_SPEED
    MOVEREL {0,0,8.5} 1
    WAIT 1000
    SETVAC gantry_head_inner 0
    WAIT 1000
    MOVEREL {0,0,-20} 10
    GOTO $PLACEEXIT